#version 450

#include "utils.glsl"

layout(local_size_x = 8, local_size_y = 8) in;

layout(rg16, binding = 0) uniform writeonly image2D out_image;



vec2 integrate_brdf(float NdotV, float roughness) {
    float A = 0.0;
    float B = 0.0;

    const vec3 N = vec3(0.0, 0.0, 1.0);
    const vec3 V = vec3(sqrt(1.0 - NdotV * NdotV), 0.0, NdotV);

    const uint SAMPLE_COUNT = 1024u;
    for(uint i = 0; i != SAMPLE_COUNT; ++i) {
        const vec2 Xi = hammersley(i, SAMPLE_COUNT);
        const vec3 H = importance_sample_ggx(Xi, N, roughness);
        const vec3 L = normalize(2.0 * dot(V, H) * H - V);

        if(L.z > 0.0) {
           const float VdotH = max(dot(V, H), 0.0);
           const float G = geometry_smith(N, V, L, roughness);
           const float G_Vis = (G * VdotH) / (max(H.z, 0.0) * NdotV);
           const float Fc = pow(1.0 - VdotH, 5.0);

            A += (1.0 - Fc) * G_Vis;
            B += Fc * G_Vis;
        }
    }

    return vec2(A, B) / float(SAMPLE_COUNT);
}



void main() {
    const vec2 size = gl_NumWorkGroups.xy * gl_WorkGroupSize.xy;
    const vec2 uv = (gl_GlobalInvocationID.xy + 0.5) / size;

    imageStore(out_image, ivec2(gl_GlobalInvocationID.xy), vec4(integrate_brdf(uv.x, uv.y), 0.0, 0.0));
}
